group 'nl.beehivejava.scriptj'
version '0.1'

apply plugin: 'java'
apply plugin: 'jacoco'

sourceCompatibility = 1.8
targetCompatibility = 1.8

sourceSets {
    integrationTest {
        java {
            compileClasspath += main.output + test.output
            runtimeClasspath += main.output + test.output
        }
    }
}

configurations {
    integrationTestCompile.extendsFrom testCompile
    integrationTestRuntime.extendsFrom testRuntime
}

dependencies {
    testCompile group: 'org.powermock', name: 'powermock-module-junit4', version: '1.6.6'
    testCompile group: 'org.powermock', name: 'powermock-api-mockito', version: '1.6.6'
    testCompile group: 'junit', name: 'junit', version: '4.12'
}

test {
    jacoco {
        append = true
        destinationFile = file("build/jacoco/test.exec")
    }

    reports.junitXml.destination = "build/test-reports"
}

task integrationTest(type: Test) {
    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    jacoco {
        append = true
        destinationFile = file("build/jacoco/test-it.exec")
    }

    reports.junitXml.destination = "build/test-reports"
}

tasks.withType(Test) {
    reports.html.destination = file("${reporting.baseDir}/${name}")
}

check.dependsOn integrationTest
integrationTest.mustRunAfter test

repositories {
    mavenCentral()
}

